program = _{ SOI ~ navigate_block+ ~ EOI }

navigate_block = { "navigateTo" ~ "(" ~ string ~ ")" ~ "{" ~ statements ~ "}" }

statements = {
  (statement ~ ";")*
}

statement = _{
  assignment_statement
  | in_statement
  | write_statement
}

expression = _{
  from_expression
  | ident
  | string
}

from_expression = { "from" ~ "(" ~ dom_query ~ ")" ~ content }

in_statement = { "in" ~ (dom_sub_query ~ "of")? ~ "(" ~ dom_query ~ ")" ~ "{" ~ statements ~ "}" }

write_statement = { "write" ~ "(" ~ expression ~ ("," ~ expression)* ~ ")" }

assignment_statement = { ident ~ "=" ~ expression }

dom_sub_query = {
  "parent"
  | "child" ~ "(" ~ dom_query ~ ")"
}

dom_query = { element ~ "having" ~ selector_tag }

selector_tag = { selector ~ "(" ~ string  ~ ")" }

selector = { "class" | "id" }

content = {
  "getTextContent"
}

element = {
  // Reminder that order might foreshadow some elements eg) th before thead will never match thead
  "_" | "form" | "input" | "span" | "h1" | "h2" | "h3" | "h4" | "h5" | "h6"
  | "div" | "table" | "thead" | "tbody" | "tr" | "th"
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }

string = _{ "'" ~ inner_str ~ "'" }

inner_str = @{ ( "''" | "\\'" | (!"'" ~ ANY) )* }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
