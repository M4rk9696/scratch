program = _{ SOI ~ navigate_block+ ~ EOI }

navigate_block = { "navigateTo" ~ "(" ~ string ~ ")" ~ "{" ~ statements ~ "}" }

statements = {
  (statement ~ ";")*
}

statement = _{
  assignment_statement
  | in_statement
  | write_statement
  // | assignment_statement
}

expression = _{
  from_expression
  | ident
  | string
}

from_expression = { "from" ~ "(" ~ element ~ "having" ~ selector_tag ~ ")" ~ content }

in_statement = { "in" ~ "parent"? ~ "(" ~ element ~ "having" ~ selector_tag ~ ")" ~ "{" ~ statements ~ "}" }

write_statement = { "write" ~ "(" ~ expression ~ ("," ~ expression)* ~ ")" }

assignment_statement = { ident ~ "=" ~ expression }

selector_tag = { selector ~ "(" ~ string  ~ ")" }

selector = { "class" | "id" }

content = {
  "getTextContent"
}

element = {
  // Reminder that order might foreshadow some elements eg) th before thead will never match thead
  "form" | "input" | "span" | "h1" | "h2" | "h3" | "h4" | "h5" | "h6"
  | "div" | "table" | "thead" | "tbody" | "tr" | "th"
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }

string = _{ "'" ~ inner_str ~ "'" }

inner_str = @{ ( "''" | "\\'" | (!"'" ~ ANY) )* }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
